/* Compilador Hell */

options {
   DEBUG_PARSER        = false;
   DEBUG_TOKEN_MANAGER = false;
}

PARSER_BEGIN(CompiladorHell) 
package parser;

import java.io.*;
import java.util.*;

import apoio.*;
import semantico.*;
import intermediario.*;
import intermediario2.*;
import destino.*;

public class CompiladorHell {
   
   public static Tabela tabela = new Tabela();
   
   public static void main(String args[]) throws ParseException {
      
      CompiladorHell analisador = null;
      try {
         analisador = new CompiladorHell(new FileInputStream(Config.nomeArquivoFonte));

		 // 1a passagem do Compilador - gera código intermediário
         ListaComandos listaComandos1aPassagem;
         listaComandos1aPassagem = CompiladorHell.inicio();
         System.out.println("Analise lexica e sintatica sem erros!");
		 System.out.println("\n-----\nTabela de simbolos: "+tabela);
         System.out.println("\n-----\nLista completa dos comandos apos a 1a passagem do compilador: \n"
                             +listaComandos1aPassagem);


		 // 2a passagem do Compilador - gera código intermediário mais proximo do assembler
		 PrimitivoListaComandos listaComandos2aPassagem;
		 listaComandos2aPassagem = listaComandos1aPassagem.geraListaPrimitivoComando();
		 System.out.println("\n-----\nLista completa dos comandos apos a 2a passagem do compilador: \n\n"
		                    +listaComandos2aPassagem);
          
		 // 3a passagem do Compilador - gera código destino - assembler (JVM do Java)
		 CodigoDestino codigoDestino = new CodigoDestino();
		 codigoDestino.geraCodigoAssembler(listaComandos2aPassagem);
         System.out.println("\n-----\nCodigo destino completo: \n\n" + codigoDestino);

		 // grava o codigo destino no arquivo .j
         BufferedWriter arqSaida;
         arqSaida = new BufferedWriter(new FileWriter(Config.nomeCodigoDestino + ".j"));
         arqSaida.write(codigoDestino.toString());
         arqSaida.close();
            
      }
      catch(FileNotFoundException e) {
         System.out.println("Erro: arquivo nao encontrado");
      }
      catch(TokenMgrError e) {
         System.out.println("Erro lexico\n" + e.getMessage());
      }
      catch(ParseException e) {
         System.out.println("Erro sintatico\n" + e.getMessage());
      }
      catch(IOException e){
		 System.out.println("Problemas na geracao do codigo destino");
		 System.exit(1);
	  }
	  catch(Exception e) {
         System.out.println(e.getMessage());
         System.exit(1);
      }
   }
}

PARSER_END(CompiladorHell)

SKIP :
{
  " " | "\r" | "\t" | "\n"
}

SKIP : { "/*" : comentarioGeral }
<comentarioGeral> SKIP: { "*/" : DEFAULT | <~[]> }

TOKEN : /* OPERADORES ARITMï¿½TICOS */
{
  < ADD : "+" >
| < SUB : "-" >
| < MULT : "*" >
| < DIV : "/" >
| < POT : "**" >
| < MOD : "%" >
}

TOKEN : /* OPERADOR DE STRING */
{
 < CONCAT : "++" >
}

TOKEN : /* OPERADORES LOGICOS E RELACIONAIS */
{
  < MAIOR : ">" >
| < MENOR : "<" >
| < MAIOR_I : ">=" >
| < MENOR_I : "<=" >
| < IGUAL : "==" >
| < DIF : "!=" >
| < AND : "E" >
| < OR : "O" >
| < NOT : "N" >
| < ANDS : "EE" >
}

TOKEN : /* PALAVRAS RESERVADAS */
{
  < IF : "SE" >
| < ELSE : "SENAO" >
| < FOR : "LACO" >
| < IN : "PEGA" >
| < OUT : "PRINTA" >
| < TEST : "TESTE" >
| < BREAK : "SAIDA" >
| < OTHERWISE : "OUTROCASO" >
| < FORMULTIPLE : "LACOMULTIPLO" >
| < FORMATRIX : "LACOMATRIX" >
| < TO : "A" >
| < STEP : "PASSO" >
}

TOKEN : /* SIMBOLOS */
{
  < VIRG : "," >
| < PT_VIRG : ";" >
| < AP : "(" >
| < FP : ")" >
| < ACH : "{" >
| < FCH : "}" >
|  < ATRIB : "=" >
}

TOKEN : /* TIPOS */
{
  < TYPENUM : "FLUT" >
| < TYPESTR : "STRING" >
| < TYPEBOOL : "BOOL" >
}

TOKEN : /* CONSTANTES */
{
  < NUM : ([ "0"-"9" ])*(".")?([ "0"-"9" ])+ >
| < STRING : "\""(~["\""])*"\"" >
| < BOOL : "true" | "false" >
}

TOKEN : /* VARIAVEL */
{
  < VAR : (["a"-"z"])+ >
}

TOKEN : /* COMENTARIO OBRIGATORIO */
{
  < COMENT : "//" (~["."])* "." >
}

/*
inicio        -> listaComandos() <EOF>
listaComandos -> (comando())* 
comando       -> 
	|comandoDeclaracoVariavel()
	|comandoAtribuicao()
	|comandoEntrada()
	|comandoPrinta()
	|comandoSe()
	|comandoTeste()
	|comandoLaco()
	|comandoLacoMultiplo()
	|comandoLacoMatrix()
*/
ListaComandos inicio() : { ListaComandos lista; }
{
	lista = listaComandos() <EOF>
	{
	   return lista;
    }	
}
ListaComandos listaComandos() : { Comando com; ListaComandos lista = new ListaComandos(); } {
	(
	  com = comando()
	  {	     if(com != null) // a declaracao de variaveis nao eh um comando: nao deve ser adicionada na lista
	     {  
	        lista.add(com);
	     }
	  }
	)*
	{	   return lista;
    }
}
Comando comando() : {Comando com = null;}
{
   (
	  comandoDeclaracaoVariavel()
    |
      com = comandoAtribuicao()
    |
      com = comandoPrinta()
    |
      com = comandoPega()
	|
	  com = comandoSe()
	|
	  com = comandoTeste()
	|
	  com = comandoLaco()
	|
	  com = comandoLacoMultiplo()
	|
	  com = comandoLacoMatrix()
   )

   {     return com;   }
}

/*
comandoDeclaracaoVariavel ->
    (
      <TYPENUM> <VAR> (<ATRIB> <NUM>)?
     ( <VIRG> <VAR> (<ATRIB> <NUM>)? )*
    | 
    <TYPESTR> <VAR> (<ATRIB> <STRING>)?
     ( <VIRG> <VAR> (<ATRIB> <STRING>)? )*
    | 
    <TYPEBOOL> <VAR> (<ATRIB> <BOOL>)?
     ( <VIRG>  <VAR> (<ATRIB> <BOOL>)? )*
    )
    <COMENT>	
*/

void comandoDeclaracaoVariavel() : {Simbolo simb; Token var;}
{
   (
     <TYPENUM>
     var=<VAR>
     {
		   simb = new Simbolo(var.image, Tipo.VAR_NUMERO, tabela.proximaReferencia()); 
		   if(CompiladorHell.tabela.isExiste(var.image))
			  AnaliseSemantica.decaracaoRepetida(var);
           else
          	  CompiladorHell.tabela.inclui(simb);
	 }
     (
       <ATRIB>
       <NUM>
       CompiladorHell.tabela.incializaSimbolo(var.image);
     )?
     (
       <VIRG>
       var=<VAR>
       {
		   simb = new Simbolo(var.image, Tipo.VAR_NUMERO, tabela.proximaReferencia());
		   if(CompiladorHell.tabela.isExiste(var.image))
			  AnaliseSemantica.decaracaoRepetida(var);
           else
          	  CompiladorHell.tabela.inclui(simb);
	   }
       (
         <ATRIB>
         <NUM>
         CompiladorHell.tabela.incializaSimbolo(var.image);
       )?
      )*
   | 
     <TYPESTR>
     var=<VAR>
     {
		   simb = new Simbolo(var.image, Tipo.VAR_STRING, tabela.proximaReferencia());
		   if(CompiladorHell.tabela.isExiste(var.image))
			  AnaliseSemantica.decaracaoRepetida(var);
           else
          	  CompiladorHell.tabela.inclui(simb);
	 }
     (
       <ATRIB>
       <STRING>
       CompiladorHell.tabela.incializaSimbolo(var.image);
     )?
     (
       <VIRG>
       var=<VAR>
       {
		  simb = new Simbolo(var.image, Tipo.VAR_STRING, tabela.proximaReferencia());
		   if(CompiladorHell.tabela.isExiste(var.image))
			  AnaliseSemantica.decaracaoRepetida(var);
           else
          	  CompiladorHell.tabela.inclui(simb);
	   }
       (
         <ATRIB>
         <STRING>
         CompiladorHell.tabela.incializaSimbolo(var.image);
       )?
     )*
   | 
     <TYPEBOOL>
     var=<VAR>
     {
		  simb = new Simbolo(var.image, Tipo.VAR_BOOLEANO, tabela.proximaReferencia());
	       CompiladorHell.tabela.inclui(simb);
	 }
     (
       <ATRIB>
       <BOOL>
       CompiladorHell.tabela.incializaSimbolo(var.image);
     )?
     (
       <VIRG>
       var=<VAR>
       {
		  simb = new Simbolo(var.image, Tipo.VAR_BOOLEANO, tabela.proximaReferencia());
	      CompiladorHell.tabela.inclui(simb);
	   }
       (
         <ATRIB>
         <BOOL>
         CompiladorHell.tabela.incializaSimbolo(var.image);
       )?
     )*
   )
   <COMENT>
}

/*
comandoAtribuicao ->  <VAR><ATRIB> exp <COMENT>
exp               ->  expLogica | expAritmetica | expString
*/
Comando comandoAtribuicao() : {ComandoAtribuicao atrib; Token var; Expressao expa;}
{
    var=<VAR>
    <ATRIB>
    expa = exp()
    {
      atrib = new ComandoAtribuicao(var.image, expa);
    }
    <COMENT>
    {
	return atrib;
    }
}


/*
comandoPrinta -> <OUT> <AP>
                 exp ((<VIRG>|<PT_VIRG>) exp)*
                 <FP> <COMENT>
*/
Comando comandoPrinta ():{ComandoPrinta printa; Expressao expa;}
{
	<OUT>
	try {
		<AP>
	}
	catch(ParseException e) {
		RecuperacaoErro.recuperaErroPanico(CompiladorHellConstants.COMENT, "abre parenteses");
		return null;
	}
	expa = exp()
	{
	  printa = new ComandoPrinta(expa);
	}
	(
	  	(
			<VIRG> | <PT_VIRG>
		)
		expa = exp()
		{
		  printa.addExpressao(expa);
		}
	)*
	try {
		<FP>
	}
	catch(ParseException e) {
		RecuperacaoErro.recuperaErroPanico(CompiladorHellConstants.COMENT, "fecha parenteses");
    }
	<COMENT>
	{
	  return printa;
	}
}

/*
comandoPega -> <IN><AP><VAR>(<VIRG><VAR>)*<FP><COMENT>
*/
Comando comandoPega (): {Token var; Pega pega = new Pega();}
{
	<IN>
	try {
		<AP>
	}
	catch(ParseException e) {
		RecuperacaoErro.recuperaErroPanico(CompiladorHellConstants.COMENT, "abre parenteses");
		return null;
	}
	var = <VAR>
	{pega.adicionaVariavel(var.image);}
	{
      if (!tabela.isExiste(var.image)) {
			System.out.println("Variavel nao declarada: \"" + var.image + "\" na linha " +
				                var.beginLine + ", coluna " + var.beginColumn);
		    System.exit(1);
      }
    }
    CompiladorHell.tabela.incializaSimbolo(var.image);
	(
	  <VIRG> var = <VAR>
	  {pega.adicionaVariavel(var.image);}
	  {
      	if (!tabela.isExiste(var.image)) {
			System.out.println("Variavel nao declarada: \"" + var.image + "\" na linha " +
				               var.beginLine + ", coluna " + var.beginColumn);
		    System.exit(1);
      	}
   	  }
   	  CompiladorHell.tabela.incializaSimbolo(var.image);
	)*
	try {
		<FP>
	}
	catch(ParseException e) {
		RecuperacaoErro.recuperaErroPanico(CompiladorHellConstants.COMENT, "fecha parenteses");
		return null;
	}
	<COMENT>
	{return pega;}
	  
}


/*
comandoSe -> <IF><AP> exp <FP>
             <ACH><COMENT>
             listaComandos <FCH>
             (<ELSE><ACH><COMENT>
             listaComandos <FCH>)?
*/

SeSenao comandoSe() : {SeSenao seSenao = new SeSenao(); Expressao expSeSenao;
                       ListaComandos listaComandosCondicaoVerdadeiro;
                       ListaComandos listaComandosCondicaoFalso; }
{  
	<IF> <AP> expSeSenao = exp() <FP>
	{
	  seSenao.setExpSeSenao(expSeSenao);
	}
	<ACH><COMENT>
	listaComandosCondicaoVerdadeiro = listaComandos() <FCH>
	{
	  seSenao.setListaComandosCondicaoVerdadeiro(listaComandosCondicaoVerdadeiro);
	}
	(
	  <ELSE><ACH><COMENT>
	  listaComandosCondicaoFalso = listaComandos()
	  {
	     seSenao.setListaComandosCondicaoFalso(listaComandosCondicaoFalso);
	  }
	  <FCH>
	)?

{ return seSenao;}

}

/*
comandoTeste() -> <TEST><ACH> (	blocoCondicional )+
            	  ( <OTHERWISE><ACH> <COMENT>
			        listaComandos()
			        <FCH> 
		          )? <FCH>

blocoCondicional -> <AP> exp <FP> <COMENT>
                    <ACH> listaComandos()
	                <FCH> (<BREAK>)? 
*/

Teste comandoTeste(): {Condicional cond; ListaComandos listaCom; Teste teste = new Teste();}
{
	<TEST><ACH>
	(
		cond = blocoCondicional()
		{teste.addCondicoes(cond); }
	)+

	(
		<OTHERWISE><ACH> <COMENT>
	    listaCom = listaComandos()
	    { teste.setOutroCaso(listaCom); }
	    <FCH> 
	)?
	<FCH>

	{return teste;}
}

Condicional blocoCondicional():{Expressao e; ListaComandos listaCom;
                                Condicional cond = new Condicional();}
{
	<AP>
	e = exp()
	{cond.setExpressao(e);}
	<FP>
	<ACH> <COMENT>
    listaCom = listaComandos()
    {cond.setListaComandos(listaCom);}
	<FCH>
	(<BREAK>)?

	{ return cond; }
	  
}

/*
comandoLaco -> <FOR> <AP> (<TYPENUM>)? <VAR><ATRIB>
               exp <PT_VIRG>
               exp <PT_VIRG>
               comandoAtribuicao <FP>
               <ACH> <COMENT> listaComandos <FCH>
*/
Comando comandoLaco () : {Simbolo simb; Token var; Token tokenTipo = null;
                          Laco laco = new Laco();
                          Expressao expressaoAuxiliar = new Expressao();
                          ListaComandos listaAuxiliar = new ListaComandos();}
{
    <FOR>
    try {
		<AP>
	}
	catch(ParseException e) {
		RecuperacaoErro.recuperaErroPanico(CompiladorHellConstants.ACH, "abre parenteses");
		return null;	
	}
    (
      tokenTipo = <TYPENUM>
    )?
    var=<VAR>
    {
	  if(tokenTipo != null)
	  {
	     simb = new Simbolo(var.image, Tipo.VAR_NUMERO, tabela.proximaReferencia());
		 if(CompiladorHell.tabela.isExiste(var.image))
			 AnaliseSemantica.decaracaoRepetida(var);
         else
          	 CompiladorHell.tabela.inclui(simb);
      }

      laco.setVarControle(var.image);
    }
    <ATRIB> expressaoAuxiliar = exp() <PT_VIRG>
    {
      	laco.setExpressaoInicializacao(expressaoAuxiliar);
  	} 
    expressaoAuxiliar = exp()
    {
      	laco.setExpressaoComparadora(expressaoAuxiliar);
    }
    <PT_VIRG> 
    <VAR>
    <ATRIB> expressaoAuxiliar = exp()
    {
      	laco.setExpressaoIteracao(expressaoAuxiliar);
    }
	
	  
    try {
		<FP>
	}
	catch(ParseException e) {
		RecuperacaoErro.recuperaErroPanico(CompiladorHellConstants.ACH, "fecha parenteses");
        return null;		                                   
	}
    <ACH> <COMENT>
    listaAuxiliar = listaComandos() <FCH>
    {
      	laco.setListaComandos(listaAuxiliar);
    }
    { return laco; }
}

/*
comandoLacoMultiplo -> <FOR_MULTIPLE> <AP> (TYPENUM)inicioLaco <FP>
                <ACH> <COMENT> listaComandos <FCH>
inicioLaco   -> <VAR> contLaco faixa
contLaco     -> <VIRG> <VAR> contLaco faixa <VIRG>
               | <PT_VIRG>
faixa -> exp <TO> exp
        (<STEP> exp)?
*/
Comando comandoLacoMultiplo() : {Token tokenTipo = null;LacoMultiplo laco;ListaComandos listaAux;}
{
	<FORMULTIPLE>
	try {
		<AP>
	}
	catch(ParseException e) {
		RecuperacaoErro.recuperaErroPanico(CompiladorHellConstants.ACH, "fecha parenteses");
        return null;	
	}
	(
      tokenTipo = <TYPENUM>
    )?
	laco = inicioLaco(tokenTipo)
	try {
		<FP>
	}
	catch(ParseException e) {
		RecuperacaoErro.recuperaErroPanico(CompiladorHellConstants.ACH, "fecha parenteses");
        return null;	
	}
	<ACH> <COMENT>
	listaAux = listaComandos()
	<FCH>
	{
	  	laco.setListaComandos(listaAux);
	  	return laco;
	}
}
LacoMultiplo inicioLaco(Token tokenTipo) : {Simbolo simb; Token var;BlocoLacoMultiplo bloco;}
{
  	
	var=<VAR>
	{
	  LacoMultiplo laco = new LacoMultiplo();
	  bloco = new BlocoLacoMultiplo(var.image);
	  laco.incluiBloco(bloco);
	  
	  if(tokenTipo!=null)
	  { 
		   simb = new Simbolo(var.image,Tipo.VAR_NUMERO, tabela.proximaReferencia());
		   if(CompiladorHell.tabela.isExiste(var.image))
			  AnaliseSemantica.decaracaoRepetida(var);
           else
          	  CompiladorHell.tabela.inclui(simb);
      }
    }
	contLaco(tokenTipo,laco) faixaLacoMultiplo(laco)

	{
	  return laco;
	}
}
void contLaco(Token tokenTipo, LacoMultiplo laco) : {Simbolo simb; Token var;BlocoLacoMultiplo bloco;}
{
	<VIRG>
	var=<VAR>
	{
	  bloco = new BlocoLacoMultiplo(var.image);
	  laco.incluiBloco(bloco);
	  if(tokenTipo!=null)
	  { 
		  simb = new Simbolo(var.image,Tipo.VAR_NUMERO, tabela.proximaReferencia());
		  if(CompiladorHell.tabela.isExiste(var.image))
			  AnaliseSemantica.decaracaoRepetida(var);
          else
          	  CompiladorHell.tabela.inclui(simb);
      }
    }
	contLaco(tokenTipo,laco) faixaLacoMultiplo(laco)<VIRG>
	|
	<PT_VIRG>
}


void faixaLacoMultiplo(LacoMultiplo laco) : {Expressao expFrom, expTo,expStep; Item auxiliar;}                                       
{

  	{
	  expFrom = new Expressao();
	  expTo = new Expressao();
	  expStep = new Expressao();
	  
	}
	expFrom = exp()
	{
	  expFrom.toString();
	}
  	
	
	<TO>
	expTo = exp()
	{
	  expTo.toString();
	  auxiliar = new Item(Tipo.CTE_NUMERO,"1");	
	  expStep.addInfixo(auxiliar);
	  expStep.addPosfixo(auxiliar);
	}

	
	
	(
	    <STEP> expStep = exp()
	    {
	    	expStep.toString();
	    }
	)?

	{
	  laco.addFaixa(expFrom,expTo,expStep); //possui um contador interno para controle de insercao das faixas
	}
}

/*
comandoLacoMatrix -> <FORMATRIX> <AP> <VAR>
                    (<VIR> <VAR>)*
	                <PT_VIRG> faixa <FP>
	                <ACH> <COMENT>
	                listaComandos <FCH>	
faixa -> exp <TO> exp
        (<STEP> exp)?
*/
Comando comandoLacoMatrix() : {Simbolo simb; Token var; Token tipo = null;
                               LacoMatrix lacoMatrix = new LacoMatrix();
                               Expressao expressaoAuxiliar = new Expressao();
                               ListaComandos listaAuxiliar = new ListaComandos();
                               ArrayList<Expressao> expressao;} 
{
      <FORMATRIX>
      try {
		<AP>
	  }
	  catch(ParseException e) {
		   RecuperacaoErro.recuperaErroPanico(CompiladorHellConstants.ACH, "fecha parenteses");
           return null;	
	  }
      (
      tipo = <TYPENUM>
      )?	
      var=<VAR>
      {
		  if(tipo!=null)
		  {
		    simb = new Simbolo(var.image,Tipo.VAR_NUMERO, tabela.proximaReferencia());
		    if(CompiladorHell.tabela.isExiste(var.image))
			   AnaliseSemantica.decaracaoRepetida(var);
            else
          	   CompiladorHell.tabela.inclui(simb);
		  }
      }
      { lacoMatrix.setVariaveisControle(var.image); }
      (
        <VIRG>
       	var=<VAR>
        {
			if(tipo!=null)
			{
			  simb = new Simbolo(var.image,Tipo.VAR_NUMERO, tabela.proximaReferencia());
		      if(CompiladorHell.tabela.isExiste(var.image))
			     AnaliseSemantica.decaracaoRepetida(var);
              else
          	     CompiladorHell.tabela.inclui(simb);
			}
    	}
    	{ lacoMatrix.setVariaveisControle(var.image); }
      )*
      <PT_VIRG>
      expressao = faixa()
	  { lacoMatrix.setExpressaoInicial(expressao.get(0)); }
	  { lacoMatrix.setExpressaoFinal(expressao.get(1)); }
	  {
	    if(expressao.size() > 2)
	  	{
	  	  lacoMatrix.setExpressaoPasso(expressao.get(2)); 
	  	}
	  }
      try {
		<FP>
	  }
	  catch(ParseException e) {
		  RecuperacaoErro.recuperaErroPanico(CompiladorHellConstants.ACH, "fecha parenteses");
          return null;	
	  }
      <ACH> <COMENT>
      listaAuxiliar = listaComandos()
      { lacoMatrix.setListaComandos(listaAuxiliar); }
      <FCH>
      { return lacoMatrix; }
}

/*
faixa -> exp <TO> exp
        (<STEP> exp)?
*/
ArrayList<Expressao> faixa(): {ArrayList<Expressao> listaExpressoes = new ArrayList<Expressao>();
                               Expressao e;}
{
   e = exp()  { listaExpressoes.add(e); }   <TO>
   e = exp() { listaExpressoes.add(e); }
   (
      <STEP>
      e = exp() { listaExpressoes.add(e); }
   )?
   {      return listaExpressoes;   }
}

/*
exp           -> expOr   ( <CONCAT>  expOr )*
expOr         -> expAnd   ( <OR>  expAnd )*
expAnd        -> expNot   ( <AND> expNot )*
expNot        -> ( <NOT> )* expRelacional
expRelacional -> expAdit  ( <MAIOR>   expAdit | <MENOR>   expAdit 
                          | <MAIOR_I> expAdit | <MENOR_I> expAdit |
                          | <IGUAL>   expAdit | <DIF>     expAdit )*
expAdit       -> expMult  ( <ADD>  expMult | <SUB> expMult  )*
expMult       -> expPot   ( <MULT> expPot  | <DIV> expPot )*
expPot        -> expToken ( <POT> expPot )?
expToken      -> <NUM> | <ADD><NUM> | <SUB><NUM> | <VAR> | <BOOL> | <STRING> 
                 | <AP> exp <FP>
*/
Expressao exp() : {Expressao e = new Expressao();}
{
    exp0(e)
    {
      e.calculaLimitStack();
      return e;
    }    
}
void exp0(Expressao e) : { Item item; Token t;}
{  
    expOr(e)
    (
      t=<CONCAT>
      {  
        item = new Item(Tipo.OPERADOR, t.image);
        e.addInfixo(item);
      }
      expOr(e)
      {  
        e.addPosfixo(item);
      }
    )*
  
}
void expOr(Expressao e) : {Item item; Token t;}
{
    expAnd(e)
    (
      t=<OR>
      {  
        item = new Item(Tipo.OPERADOR, t.image);
        e.addInfixo(item);
      }
      expAnd(e)
      {  
        e.addPosfixo(item);
      }
    )*
}
void expAnd(Expressao e) : {Item item; Token t;}
{
    expAnds(e)
    (
       t=<AND>
       {  
          item = new Item(Tipo.OPERADOR, t.image);
          e.addInfixo(item);
       }
       expAnds(e)
      {  
        e.addPosfixo(item);
      }
    )*
}
void expAnds(Expressao e) : {Item item; Token t;}
{
    expNot(e)
    (
       t=<ANDS>
       {  
	       item = new Item(Tipo.OPERADOR, t.image);
	       e.addInfixo(item);
       }

       expNot(e)
       {  
        e.addPosfixo(item);
      }
    )*
}
void expNot(Expressao e) : {Item item = null; Token t; int cont = 0;}
{
    (
       t=<NOT>
       {  
	       item = new Item(Tipo.OPERADOR, t.image);
	       e.addInfixo(item);
	       cont++;
       }
    )*
    expRelacional(e)
    {  
        while(cont-- > 0)
           e.addPosfixo(item);
    }
}
void expRelacional(Expressao e) : {Item item; Token t;}
{
   expAdit(e)
   (
       t=<MAIOR>
       {  
	       item = new Item(Tipo.OPERADOR, t.image);
	       e.addInfixo(item);
       }

       expAdit(e)
       {  
           e.addPosfixo(item);
       }
     |
       t=<MENOR>
       {  
	       item = new Item(Tipo.OPERADOR, t.image);
	       e.addInfixo(item);
       }

       expAdit(e)
       {  
           e.addPosfixo(item);
       }
     |
       t=<MAIOR_I>
       {  
	       item = new Item(Tipo.OPERADOR, t.image);
	       e.addInfixo(item);
       }

       expAdit(e)
       {  
           e.addPosfixo(item);
       }
       
     |
       t=<MENOR_I>
       {  
	       item = new Item(Tipo.OPERADOR, t.image);
	       e.addInfixo(item);
       }

       expAdit(e)
       {  
           e.addPosfixo(item);
       }
     |
      t=<IGUAL>
      {  
	       item = new Item(Tipo.OPERADOR, t.image);
	       e.addInfixo(item);
      }

      expAdit(e)
      {  
           e.addPosfixo(item);
      }
     |
      t=<DIF>
      {  
	       item = new Item(Tipo.OPERADOR, t.image);
	       e.addInfixo(item);
      }
      expAdit(e)
      {  
           e.addPosfixo(item);
      }
   )*
}
void expAdit(Expressao e) : {Item item; Token t;}
{
	expMult(e)
	(
	     t=<ADD>
         {  
	         item = new Item(Tipo.OPERADOR, t.image);
	         e.addInfixo(item);
         }
	     expMult(e)
         {  
             e.addPosfixo(item);
         }
	  |
	     t=<SUB>
         {  
	         item = new Item(Tipo.OPERADOR, t.image);
	         e.addInfixo(item);
         }
         expMult(e)
         {  
             e.addPosfixo(item);
         }
	)*
}  
void expMult(Expressao e) : {Item item; Token t;}
{
	expPot(e)
	(
	   t=<MULT>
       {  
	       item = new Item(Tipo.OPERADOR, t.image);
	       e.addInfixo(item);
       }

	   expPot(e)
       {  
           e.addPosfixo(item);
       }  
	|
	   t=<DIV>
       {  
	       item = new Item(Tipo.OPERADOR, t.image);
	       e.addInfixo(item);
       }

	   expPot(e)
       {  
           e.addPosfixo(item);
       }
	)*
}
void expPot(Expressao e) : {Item item; Token t;}
{
	expToken(e)
	(
	  t=<POT>
	  {  
	        item = new Item(Tipo.OPERADOR, t.image);
            e.addInfixo(item);
	  } 
	  expPot(e)
	  {  
            e.addPosfixo(item);
	  }  
	)?
}
void expToken(Expressao e) :{Item item; Token t; String tConcatenada;Tipo tipo;}
{
      t = <NUM>
      {
         item = new Item(Tipo.CTE_NUMERO, t.image);
         e.addPosfixo(item);
         e.addInfixo(item);
      }  
      
    |
      t=<ADD>
      { tConcatenada = t.image; }      t=<NUM>
      {
         tConcatenada = tConcatenada + t.image; 
         item = new Item(Tipo.CTE_NUMERO, tConcatenada);
         e.addPosfixo(item);
         e.addInfixo(item);
      }
      
    |
      t=<SUB>
      { tConcatenada = t.image; }
      t=<NUM>
      {
         tConcatenada = tConcatenada + t.image; 
         item = new Item(Tipo.CTE_NUMERO, tConcatenada);
         e.addPosfixo(item);
         e.addInfixo(item);
      }
    |
      t=<VAR>  
      {
	      if (!tabela.isExiste(t.image)) {
			 System.out.println("Variavel nao declarada: \"" + t.image + "\" na linha " +
				                t.beginLine + ", coluna " + t.beginColumn);
			 System.exit(1);
          }
          tipo = tabela.consultaTipo(t.image);
          item = new Item(tipo,t.image);
          e.addPosfixo(item);
          e.addInfixo(item);
      }
    |
      t=<BOOL>
      {
        item = new Item(Tipo.CTE_BOOLEANO, t.image);
        e.addPosfixo(item);
        e.addInfixo(item);
      }
    |
      t=<STRING>
      {
        item = new Item(Tipo.CTE_STRING, t.image);
        e.addPosfixo(item);
        e.addInfixo(item);
      }
    |
      t=<AP>
      {
        item = new Item(Tipo.PARENTESES, t.image);
        e.addInfixo(item);
      }
      exp0(e)
      
      t=<FP>
	  {
        item = new Item(Tipo.PARENTESES, t.image);
        e.addInfixo(item);
      } 
 }

