/* Compilador Hell */

options {
   DEBUG_PARSER        = false;
   DEBUG_TOKEN_MANAGER = false;
}

PARSER_BEGIN(CompiladorHell)
package parser;

import java.io.*;
import apoio.*;
import semantico.*;
import intermediario.*;

public class CompiladorHell {
   
   public static Tabela tabela = new Tabela();
   
   public static void main(String args[]) throws ParseException {
      
      CompiladorHell analisador = null;
      try {
         analisador = new CompiladorHell(new FileInputStream(Config.nomeArquivoFonte));
         CompiladorHell.inicio();
          System.out.println("Analise lexica e sintatica sem erros!");

		  System.out.println(tabela);
          
      }
      catch(FileNotFoundException e) {
         System.out.println("Erro: arquivo nao encontrado");
      }
      catch(TokenMgrError e) {
         System.out.println("Erro lexico\n" + e.getMessage());
      }
      catch(ParseException e) {
         System.out.println("Erro sintatico\n" + e.getMessage());
      }
   }
}

PARSER_END(CompiladorHell)

SKIP :
{
  " " | "\r" | "\t" | "\n"
}

SKIP : { "/*" : comentarioGeral }
<comentarioGeral> SKIP: { "*/" : DEFAULT | <~[]> }

TOKEN : /* OPERADORES ARITM�TICOS */
{
  < ADD : "+" >
| < SUB : "-" >
| < MULT : "*" >
| < DIV : "/" >
| < POT : "**" >
| < MOD : "%" >
}

TOKEN : /* OPERADOR DE STRING */
{
 < CONCAT : "++" >
}

TOKEN : /* OPERADORES LOGICOS E RELACIONAIS */
{
  < MAIOR : ">" >
| < MENOR : "<" >
| < MAIOR_I : ">=" >
| < MENOR_I : "<=" >
| < IGUAL : "==" >
| < DIF : "!=" >
| < AND : "E" >
| < OR : "O" >
| < NOT : "N" >
| < ANDS : "EE" >
}

TOKEN : /* PALAVRAS RESERVADAS */
{
  < IF : "SE" >
| < ELSE : "SENAO" >
| < FOR : "LACO" >
| < IN : "PEGA" >
| < OUT : "PRINTA" >
| < TEST : "TESTE" >
| < BREAK : "SAIDA" >
| < OTHERWISE : "OUTROCASO" >
| < FORMULTIPLE : "LACOMULTIPLO" >
| < FORMATRIX : "LACOMATRIX" >
| < TO : "A" >
| < STEP : "PASSO" >
}

TOKEN : /* SIMBOLOS */
{
  < VIRG : "," >
| < PT_VIRG : ";" >
| < AP : "(" >
| < FP : ")" >
| < ACH : "{" >
| < FCH : "}" >
|  < ATRIB : "=" >
}

TOKEN : /* TIPOS */
{
  < TYPENUM : "FLUT" >
| < TYPESTR : "STRING" >
| < TYPEBOOL : "BOOL" >
}

TOKEN : /* CONSTANTES */
{
  < NUM : ([ "0"-"9" ])*(".")?([ "0"-"9" ])+ >
| < STRING : "\""(~["\""])*"\"" >
| < BOOL : "true" | "false" >
}

TOKEN : /* VARIAVEL */
{
  < VAR : (["a"-"z"])+ >
}

TOKEN : /* COMENTARIO OBRIGAT�RIO */
{
  < COMENT : "//" (~["."])* "." >
}

/*
inicio        -> listaComandos() <EOF>
listaComandos -> (comando())* 
comando       -> 
	|comandoDeclaracoVariavel()
	|comandoAtribuicao()
	|comandoEntrada()
	|comandoPrinta()
	|comandoSe()
	|comandoTeste()
	|comandoLaco()
	|comandoLacoMultiplo()
	|comandoLacoMatrix()
*/
void inicio() : {} {
	listaComandos() <EOF>
}
void listaComandos() : {} {
	(comando())* 
}
void comando() : {} {
	  comandoDeclaracaoVariavel()
    |
      comandoAtribuicao()
    |
      comandoPrinta()
    |
      comandoPega()
	|
	  comandoSe()
	|
	  comandoTeste()
	|
	  comandoLaco()
	|
	  comandoLacoMultiplo()
	|
	  comandoLacoMatrix()
}

/*
comandoDeclaracaoVariavel ->
    (
      <TYPENUM> <VAR> (<ATRIB> <NUM>)?
     ( <VIRG> <VAR> (<ATRIB> <NUM>)? )*
    | 
    <TYPESTR> <VAR> (<ATRIB> <STRING>)?
     ( <VIRG> <VAR> (<ATRIB> <STRING>)? )*
    | 
    <TYPEBOOL> <VAR> (<ATRIB> <BOOL>)?
     ( <VIRG>  <VAR> (<ATRIB> <BOOL>)? )*
    )
    <COMENT>	
*/

void comandoDeclaracaoVariavel() : {Simbolo simb; Token var;}
{
   (
     <TYPENUM>
     var=<VAR>
     {
		   simb = new Simbolo(var.image, Tipo.VAR_NUMERO);
	       CompiladorHell.tabela.inclui(simb);
	 }
     (
       <ATRIB>
       <NUM>
     )?
     (
       <VIRG>
       var=<VAR>
       {
		   simb = new Simbolo(var.image, Tipo.VAR_NUMERO);
	       CompiladorHell.tabela.inclui(simb);
	   }
       (
         <ATRIB>
         <NUM>
       )?
      )*
   | 
     <TYPESTR>
     var=<VAR>
     {
		   simb = new Simbolo(var.image, Tipo.VAR_STRING);
	       CompiladorHell.tabela.inclui(simb);
	 }
     (
       <ATRIB>
       <STRING>
     )?
     (
       <VIRG>
       var=<VAR>
       {
		  simb = new Simbolo(var.image, Tipo.VAR_STRING);
	       CompiladorHell.tabela.inclui(simb);
	   }
       (
         <ATRIB>
         <STRING>
       )?
     )*
   | 
     <TYPEBOOL>
     var=<VAR>
     {
		  simb = new Simbolo(var.image, Tipo.VAR_BOOLEANO);
	       CompiladorHell.tabela.inclui(simb);
	 }
     (
       <ATRIB>
       <BOOL>
     )?
     (
       <VIRG>
       var=<VAR>
       {
		  simb = new Simbolo(var.image, Tipo.VAR_BOOLEANO);
	      CompiladorHell.tabela.inclui(simb);
	   }
       (
         <ATRIB>
         <BOOL>
       )?
     )*
   )
   <COMENT>	
}

/*
comandoAtribui��o ->  <VAR><ATRIB> exp <COMENT>
exp               ->  expLogica | expAritmetica | expString
*/
Comando comandoAtribuicao() : {ComandoAtribuicao atrib; Token var; Expressao expa;}
{
    var=<VAR>
    <ATRIB>
    expa = exp()
    {
      atrib = new ComandoAtribuicao(var.image, expa);
    }
    <COMENT>
    {
	return atrib;
    }
}


/*
comandoPrinta -> <OUT> <AP>
                 exp ((<VIRG>|<PT_VIRG>) exp)*
                 <FP> <COMENT>
*/
void comandoPrinta ():{}
{
	<OUT>
	try {
		<AP>
	}
	catch(ParseException e) {
		RecuperacaoErro.recuperaErroPanico(CompiladorHellConstants.COMENT, "abre parenteses");
		return;
	}
	exp()
	(
	  	(
			<VIRG> | <PT_VIRG>
		)
		exp()
	)*
	try {
		<FP>
	}
	catch(ParseException e) {
		RecuperacaoErro.recuperaErroPanico(CompiladorHellConstants.COMENT, "fecha parenteses");
		return;
    }
	<COMENT>
}

/*
comandoPega -> <IN><AP><VAR>(<VIRG><VAR>)*<FP><COMENT>
*/
void comandoPega (): {}
{
	<IN>
	try {
		<AP>
	}
	catch(ParseException e) {
		RecuperacaoErro.recuperaErroPanico(CompiladorHellConstants.COMENT, "abre parenteses");
		return;
	}
	<VAR>
	(
	  <VIRG><VAR>
	)*
	try {
		<FP>
	}
	catch(ParseException e) {
		RecuperacaoErro.recuperaErroPanico(CompiladorHellConstants.COMENT, "fecha parenteses");
		return;
	}
	<COMENT>	
}

/*
comandoSe -> <IF><AP> exp <FP>
             <ACH><COMENT>
             listaComandos <FCH>
             (<ELSE><ACH><COMENT>
             listaComandos <FCH>)?
*/
void comandoSe() : {}
{  
	<IF> <AP> exp() <FP>
	<ACH><COMENT>
	listaComandos() <FCH>
	(
	  <ELSE><ACH><COMENT>
	  listaComandos()
	  <FCH>
	)?
}

/*
comandoTeste() -> <TEST><ACH> (	blocoCondicional )+
            	  ( <OTHERWISE><ACH> <COMENT>
			        listaComandos()
			        <FCH> 
		          )? <FCH>

blocoCondicional -> <AP> exp <FP> <COMENT>
                    <ACH> listaComandos()
	                <FCH> (<BREAK>)? 
*/
void comandoTeste(): {}{
	<TEST><ACH>
	(
		blocoCondicional()
	)+

	(
		<OTHERWISE><ACH> <COMENT>
	    listaComandos()
	    <FCH> 
	)?
	<FCH>
}

void blocoCondicional():{}
{
	<AP>exp()<FP>
	<ACH> <COMENT>
    listaComandos()
	<FCH>
	(<BREAK>)? 
}


/*
comandoLaco -> <FOR> <AP> (<TYPENUM>)? <VAR><ATRIB>
               exp <PT_VIRG>
               exp <PT_VIRG>
               comandoAtribuicao <FP>
               <ACH> <COMENT> listaComandos <FCH>
*/
void comandoLaco () : {Simbolo simb; Token var; Token tokenTipo = null;}
{
    <FOR>
    try {
		<AP>
	}
	catch(ParseException e) {
		RecuperacaoErro.recuperaErroPanico(CompiladorHellConstants.ACH, "abre parenteses");
		return;	
	}
    (
      tokenTipo = <TYPENUM>
    )?
    var=<VAR>
    {
	  if(tokenTipo != null)
	  {
	     simb = new Simbolo(var.image, Tipo.VAR_NUMERO);
	      CompiladorHell.tabela.inclui(simb);
      }
    }
    <ATRIB> exp() <PT_VIRG>
    exp()
    <PT_VIRG> 
    <VAR>
    <ATRIB> exp()
    try {
		<FP>
	}
	catch(ParseException e) {
		RecuperacaoErro.recuperaErroPanico(CompiladorHellConstants.ACH, "fecha parenteses");
        return;		                                   
	}
    <ACH> <COMENT>
    listaComandos() <FCH>
}

/*
comandoLacoMultiplo -> <FOR_MULTIPLE> <AP> (TYPENUM)inicioLaco <FP>
                <ACH> <COMENT> listaComandos <FCH>
inicioLaco   -> <VAR> contLaco faixa
contLaco     -> <VIRG> <VAR> contLaco faixa <VIRG>
               | <PT_VIRG>
faixa -> exp <TO> exp
        (<STEP> exp)?
*/
void comandoLacoMultiplo() : {Token tokenTipo = null;}
{
	<FORMULTIPLE>
	try {
		<AP>
	}
	catch(ParseException e) {
		RecuperacaoErro.recuperaErroPanico(CompiladorHellConstants.ACH, "fecha parenteses");
        return;	
	}
	(
      tokenTipo = <TYPENUM>
    )?
	inicioLaco(tokenTipo)
	try {
		<FP>
	}
	catch(ParseException e) {
		RecuperacaoErro.recuperaErroPanico(CompiladorHellConstants.ACH, "fecha parenteses");
        return;	
	}
	<ACH> <COMENT>
	listaComandos()
	<FCH>
}
void inicioLaco(Token tokenTipo) : {Simbolo simb; Token var;}
{
	var=<VAR>
	{
	  if(tokenTipo!=null)
	  { 
		   simb = new Simbolo(var.image,Tipo.VAR_NUMERO);
	       CompiladorHell.tabela.inclui(simb);
      }
    }
	contLaco(tokenTipo) faixa()
}
void contLaco(Token tokenTipo) : {Simbolo simb; Token var;}
{
	<VIRG>
	var=<VAR>
	{
	  if(tokenTipo!=null)
	  { 
		  simb = new Simbolo(var.image,Tipo.VAR_NUMERO);
	      CompiladorHell.tabela.inclui(simb);
      }
    }
	contLaco(tokenTipo) faixa() <VIRG> 
	|
	<PT_VIRG>
}
void faixa() : {}
{
	exp()
	<TO>
	exp()
	(
	    <STEP> exp()
	)?
}

/*
comandoLacoMatrix -> <FORMATRIX> <AP> <VAR>
                    (<VIR> <VAR>)*
	                <PT_VIRG> faixa <FP>
	                <ACH> <COMENT>
	                listaComandos <FCH>	
faixa -> expAritmetica <TO> expAritmetica
        (<STEP> expAritmetica)?
*/
void comandoLacoMatrix() : {Simbolo simb; Token var; Token tipo = null;} 
{
      <FORMATRIX>
      try {
		<AP>
	  }
	  catch(ParseException e) {
		   RecuperacaoErro.recuperaErroPanico(CompiladorHellConstants.ACH, "fecha parenteses");
        return;	
	  }
      (
      tipo = <TYPENUM>
      )?	
      var=<VAR>
      {
		  if(tipo!=null)
		  {
		    simb = new Simbolo(var.image,Tipo.VAR_NUMERO);
	      	 CompiladorHell.tabela.inclui(simb);
		  }
      }
      (
        <VIRG>
       	var=<VAR>
        {
			if(tipo!=null)
			{
			  simb = new Simbolo(var.image,Tipo.VAR_NUMERO);
	      	   CompiladorHell.tabela.inclui(simb);
			}
    	}
      )*
      <PT_VIRG>
      faixa()
      try {
		<FP>
	  }
	  catch(ParseException e) {
		  RecuperacaoErro.recuperaErroPanico(CompiladorHellConstants.ACH, "fecha parenteses");
        return;	
	  }
      <ACH> <COMENT>
      listaComandos()
      <FCH>
}

/*
exp           -> expOr   ( <CONCAT>  expOr )*
expOr         -> expAnd   ( <OR>  expAnd )*
expAnd        -> expNot   ( <AND> expNot )*
expNot        -> ( <NOT> )* expRelacional
expRelacional -> expAdit  ( <MAIOR>   expAdit | <MENOR>   expAdit 
                          | <MAIOR_I> expAdit | <MENOR_I> expAdit |
                          | <IGUAL>   expAdit | <DIF>     expAdit )*
expAdit       -> expMult  ( <ADD>  expMult | <SUB> expMult  )*
expMult       -> expPot   ( <MULT> expPot  | <DIV> expPot )*
expPot        -> expToken ( <POT> expPot )?
expToken      -> <NUM> | <ADD><NUM> | <SUB><NUM> | <VAR> | <BOOL> | <STRING> 
                 | <AP> exp <FP>
*/
void exp() : {}
{
    expOr()
    (
      <CONCAT> expOr()
    )*
}
void expOr() : {}
{
    expAnd()
    (
      <OR> expAnd()
    )*
}
void expAnd() : {}
{
    expAnds()
    (
       <AND> expAnds()
    )*
}
void expAnds() : {}
{
    expNot()
    (
       <ANDS> expNot()
    )*
}
void expNot() : {}
{
    (
       <NOT>
    )*
    expRelacional()
}
void expRelacional() : {}
{
   expAdit()
   (
       <MAIOR>   expAdit()     
     |
       <MENOR>   expAdit()     
     |
       <MAIOR_I> expAdit()
       
     |
       <MENOR_I> expAdit()    
     |
       <IGUAL>   expAdit()     
     |
       <DIF>     expAdit()  
   )*
}
void expAdit() : {}
{
	expMult()
	(
	  <ADD> expMult()
	  |
	  <SUB> expMult()
	 )*
}  
void expMult() : {}
{
	expPot()
	(
	   <MULT> expPot()
	 |
	   <DIV>  expPot()
	)*
}
void expPot() : {}
{
	expToken()
	(
	  <POT> expPot()
	)?
}
void expToken() :{Simbolo simb; Token t;}
{
    <NUM>
    |
      <ADD>
      <NUM>
    |
      <SUB>
      <NUM>
    |
      <VAR>
    |
      <BOOL>
    |
      <STRING>
    |
    <AP> exp()<FP>
}
